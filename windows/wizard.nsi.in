!define APPNAME "NPG-explorer"
!define COMPANYNAME "Department of Mathematical Methods in Biology"
!define DESCRIPTION "Nucleotide PanGenome explorer"
!define VERSIONMAJOR @VERSION_MAJOR@
!define VERSIONMINOR @VERSION_MINOR@
!define VERSIONBUILD @VERSION_PATCH@
!define HELPURL "https://bitbucket.org/starius/npg-explorer/issues/new"
!define UPDATEURL "https://bitbucket.org/starius/npg-explorer/downloads"
!define ABOUTURL "http://mouse.belozersky.msu.ru/tools/npge.html"

!define INSTALLSIZE 16000

RequestExecutionLevel admin

InstallDir "$PROGRAMFILES\${APPNAME}"

# rtf or txt file - remember if it is txt, \
# it must be in the DOS text format (\r\n)
LicenseData "LICENSE.txt"
Name "${COMPANYNAME} - ${APPNAME}"
Icon "npge.ico"
outFile "npge_@VERSION@_win32.exe"

!include LogicLib.nsh

page license
page directory
Page instfiles

!macro VerifyUserIsAdmin
UserInfo::GetAccountType
pop $0
${If} $0 != "admin" ;Require admin rights on NT4+
    messageBox mb_iconstop "Administrator rights required!"
    setErrorLevel 740 ;ERROR_ELEVATION_REQUIRED
    quit
${EndIf}
!macroend

function .onInit
    setShellVarContext all
    !insertmacro VerifyUserIsAdmin
functionEnd

section "install"
    setOutPath $INSTDIR
    file "npge.exe"
    file "qnpge.exe"
    file "makeblastdb.exe"
    file "blastn.exe"
    file "vcomp100.dll"
    file "genomes.tsv"
    file "npge.ico"
    file "README.txt"
    file "README.html"
    file "AllOptions.html"
    file "AllProcessors.html"
    file "LICENSE.txt"
    file "AUTHORS.txt"

    writeUninstaller "$INSTDIR\uninstall.exe"

    # Start Menu
    createDirectory "$SMPROGRAMS\${APPNAME}"
    createShortCut "$SMPROGRAMS\${APPNAME}\${APPNAME}.lnk" \
        "$INSTDIR\npge.exe" "" "$INSTDIR\npge.ico"

    # Registry information for add/remove programs
    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} \
    ${APPNAME}" "DisplayName" "${APPNAME} - ${DESCRIPTION}"

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} \
    ${APPNAME}" "UninstallString" \
    "$\"$INSTDIR\uninstall.exe$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "InstallLocation" "$\"$INSTDIR$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "DisplayIcon" "$\"$INSTDIR\npge.ico$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "Publisher" "$\"${COMPANYNAME}$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "HelpLink" "$\"${HELPURL}$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "URLUpdateInfo" "$\"${UPDATEURL}$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "URLInfoAbout" "$\"${ABOUTURL}$\""

    WriteRegStr HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "DisplayVersion" \
    "$\"${VERSIONMAJOR}.${VERSIONMINOR}.${VERSIONBUILD}$\""

    WriteRegDWORD HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "VersionMajor" ${VERSIONMAJOR}

    WriteRegDWORD HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "VersionMinor" ${VERSIONMINOR}

    # There is no option for modifying or repairing the install
    WriteRegDWORD HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "NoModify" 1

    WriteRegDWORD HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "NoRepair" 1

    # Set the INSTALLSIZE constant
    # (!defined at the top of this script)
    # so Add/Remove Programs can accurately report the size
    WriteRegDWORD HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" \
    "EstimatedSize" ${INSTALLSIZE}
sectionEnd

# Uninstaller

function un.onInit
    SetShellVarContext all

    #Verify the uninstaller - last chance to back out
    MessageBox MB_OKCANCEL \
    "Permanantly remove ${APPNAME}?" IDOK next
        Abort
    next:
    !insertmacro VerifyUserIsAdmin
functionEnd

section "uninstall"
    # Remove Start Menu launcher
    delete "$SMPROGRAMS\${APPNAME}\${APPNAME}.lnk"
    # Try to remove the Start Menu folder -
    # this will only happen if it is empty
    rmDir "$SMPROGRAMS\${APPNAME}"

    # Remove files
    delete $INSTDIR\npge.exe
    delete $INSTDIR\qnpge.exe
    delete $INSTDIR\makeblastdb.exe
    delete $INSTDIR\blastn.exe
    delete $INSTDIR\vcomp100.dll
    delete $INSTDIR\genomes.tsv
    delete $INSTDIR\npge.ico
    delete $INSTDIR\README.txt
    delete $INSTDIR\README.html
    delete $INSTDIR\AllOptions.html
    delete $INSTDIR\AllProcessors.html
    delete $INSTDIR\LICENSE.txt
    delete $INSTDIR\AUTHORS.txt
    delete $INSTDIR\npge.ico

    # Always delete uninstaller as the last action
    delete $INSTDIR\uninstall.exe

    # Try to remove the install directory - this will only happen if it is empty
    rmDir $INSTDIR

    # Remove uninstaller information from the registry
    DeleteRegKey HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}"
sectionEnd

